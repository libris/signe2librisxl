prefix : <https://id.kb.se/vocab/>
prefix madsrdf: <http://www.loc.gov/mads/rdf/v1#>
prefix skos: <http://www.w3.org/2004/02/skos/core#>

# Run with these as input:
# frequencies.ttl
# https://id.loc.gov/vocabulary/frequencies.rdf
# http://www.rdaregistry.info/xml/termList/frequency.xml
# https://www.dublincore.org/specifications/dublin-core/collection-description/frequency/freq.rdf

construct {
    ?narrowFreq a :Frequency ;
        ?broaderOrExact ?kbFreq ;
        :label ?label .

    ?kbFreq a :Frequency ;
        ?closeOrExact ?lcFreq, ?rdaFreq, ?dcFreq ;
        :label ?freqLabel . #?multiLangLabel ;
#        :definition ?definition .
} where {
    {
        values ( ?freqShortId ?kbFreqLabel ?signeFreeText ) {
            ( "dyl" "four times a week" "4/v" )
            ( "dyl" "five times a week" "5/v" )
            ( "dyl" "six times a week" "6/v" )
            ( "bmn" "six times a year" "6/år" )
            ( "bmn" "seven times a year" "7/år" )
            ( "bmn" "eight times a year" "8/år" )
            ( "mon" "ten times a year" "10/år" )
            ( "mon" "twelve times a year" "12/år" )
        }
        bind(iri(concat("http://id.loc.gov/vocabulary/frequencies/", ?freqShortId)) as ?lcFreq)
    } union {
        values ( ?freqShortId ?signeFreeText ) {
            ( "wkl" "1/v" )
            ( "mon" "1/m" )
            ( "ann" "1/år" )
            ( "swk" "2/v" )
            ( "smn" "2/m" )
            ( "san" "2/år" )
            ( "ttw" "3/v" )
            ( "ttm" "3/m" )
            ( "tty" "3/år" )
            ( "dyl" "7/v" )
        }
        bind(iri(concat("http://id.loc.gov/vocabulary/frequencies/", ?freqShortId)) as ?lcFreq)
        ?lcFreq madsrdf:authoritativeLabel ?lcLabel .
    }

    bind(strlang(coalesce(?lcLabel, ?kbFreqLabel), 'en') as ?freqLabel)
    bind(if(bound(?lcLabel), :exactMatch, :closeMatch) as ?closeOrExact)

    optional {
        {
            ?lcFreq madsrdf:authoritativeLabel ?lcLabelToMatch .
            bind(ucase(substr(?lcLabelToMatch, 1, 1)) as ?initial)
            bind(substr(?lcLabelToMatch, 2) as ?rest)
            bind(strlang(concat(?initial, ?rest), "en") as ?capitalized)
            ?dcFreq skos:prefLabel ?capitalized .
        } union {
            ?lcFreq madsrdf:authoritativeLabel ?lcLabelToMatch .
            bind(strlang(?lcLabelToMatch, 'en') as ?enLabel)
            ?rdaFreq skos:prefLabel ?enLabel, ?rdaLabel ;
                skos:definition ?rdaDefinition .
        }
    }

#    bind(if(?closeOrExact = :exactMatch, ?rdaDefinition, ?unbound) as ?definition)
#    bind(if(?closeOrExact = :exactMatch, ?rdaLabel, ?unbound) as ?multiLangLabel)

    bind(replace(?freqLabel, "\\s+", "") as ?trimmedLabel)
    bind(iri(concat("https://id.kb.se/frequencies/", ?trimmedLabel)) as ?kbFreq)

    ?narrowFreq a :Frequency ;
        :label ?label .

    bind(replace(?label, " -.*", "") as ?freqPart)

    filter(?freqPart = ?signeFreeText)
    bind(if(?label = ?signeFreeText, :exactMatch, :broader) as ?broaderOrExact)
}